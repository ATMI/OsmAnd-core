apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "20"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    // This is from OsmAndCore_android.aar - for some reason it's not inherited
    aaptOptions {
        // Don't compress any embedded resources
        noCompress "qz"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            jni.srcDirs = []
            jniLibs.srcDirs = ["libs"]
            java.srcDirs = ["src"]
            resources.srcDirs = ["src"]
            renderscript.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }
    }

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        mips {
            ndk {
                abiFilter "mips"
            }
        }
        armv7 {
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        armv5 {
            ndk {
                abiFilter "armeabi"
            }
        }
        fat
    }
}

repositories {
    flatDir {
        dirs "../../../wrappers/android/build/outputs/aar"
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:21.0.2"
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile "net.osmand.core:OsmAndCore_android-release:1.0@aar"
}
