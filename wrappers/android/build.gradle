buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.12.2'
	}
}

allprojects {
	repositories {
		mavenCentral()
	}
}

apply plugin: 'com.android.library'

android {
	sourceSets.main {
		manifest.srcFile "AndroidManifest.xml"
		java.srcDirs = ["src", "./gen/java"]
		jniLibs.srcDirs = ["./jniLibs"]
		jni.srcDirs = []
		assets.srcDirs = ['assets']
	}

	compileSdkVersion 19
	buildToolsVersion "20"

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion 19
	}

	buildTypes {
		release {
		}
	}

	lintOptions {
		abortOnError false
	}
}

// Java sources generation task
import org.apache.tools.ant.taskdefs.condition.Os
task swigGenerateJava(type: Exec) {
	description "Generate SWIG Java interface"

	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		commandLine "cmd", "/c", "bash --login "+file("../java/generate.sh").getAbsolutePath()+" "+projectDir.getAbsolutePath()
	} else {
		commandLine "sh", file("../java/generate.sh").getAbsolutePath(), projectDir.getAbsolutePath()
	}
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << swigGenerateJava
}

// OsmAndCore JNI build task
import org.apache.tools.ant.taskdefs.condition.Os
task buildOsmAndCore(type: Exec) {
	description "Build OsmAndCore"

	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		commandLine file("build.cmd").getAbsolutePath(), "release"
	} else {
		commandLine "sh", file("build.sh").getAbsolutePath(), "release"
	}
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << buildOsmAndCore
}

// NDK libraries copy task
task copyNdkSharedLibs(type: Copy) {
	description "Copy NDK shared libraries"

	from("$System.env.ANDROID_NDK/sources/cxx-stl/gnu-libstdc++/4.9/libs") {
		include "armeabi/libgnustl_shared.so"
		include "armeabi-v7a/libgnustl_shared.so"
		include "mips/libgnustl_shared.so"
		include "x86/libgnustl_shared.so"
	}

	into "./jniLibs"
	includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << copyNdkSharedLibs
}

// OsmAnd libraries copy task
task copyOsmAndSharedLibs(type: Copy) {
	description "Copy OsmAnd shared libraries"

	dependsOn buildOsmAndCore

	from("../../../binaries/android") {
		include "gcc-*/Release/libOsmAndCore_shared.so"
		include "gcc-*/Release/libOsmAndCoreJNI.so"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = fileCopyDetails.path.replaceAll(/.*gcc-(.*)\/Release\/(.*)/, '$1/$2')
		}
	}

	into "./jniLibs"
	includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << copyOsmAndSharedLibs
}

// Qt shared libraries copy task
task copyQtSharedLibs(type: Copy) {
	description "Copy Qt shared libraries"

	dependsOn buildOsmAndCore

	from("../../externals/qtbase-android") {
		include "upstream.patched.android.gcc-*.shared/lib/libQt5Core.so"
		include "upstream.patched.android.gcc-*.shared/lib/libQt5Network.so"
		include "upstream.patched.android.gcc-*.shared/lib/libQt5Sql.so"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = fileCopyDetails.path.replaceAll(/.*upstream\.patched\.android\.gcc\-(.*)\.shared\/lib\/(.*)/, '$1/$2')
		}
	}

	into "./jniLibs"
	includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << copyQtSharedLibs
}

// Qt jar libraries copy task
task copyQtJarLibs(type: Copy) {
	description "Copy Qt JAR libraries"

	dependsOn buildOsmAndCore

	from("../../externals/qtbase-android") {
		include "upstream.patched.android.gcc-*.shared/jar/*-bundled.jar"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = fileCopyDetails.path.replaceAll(/.*upstream\.patched\.android\.gcc\-.*\.shared\/jar\/(.*)/, '$1')
		}
	}

	into "./libs"
	includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << copyQtJarLibs
}

// OsmAnd resources copy task
task copyOsmAndResources(type: Copy) {
	description "Copy OsmAnd resources"

	// Map styles and related:
	from("../../../resources/rendering_styles") {
		include "default.render.xml"
		include "default.map_styles_presets.xml"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "map/styles/" + fileCopyDetails.path
		}
	}

	// Map icons (Android mdpi == 1.0 ddf):
	from("../../../resources/rendering_styles/style-icons/drawable-mdpi") {
		include "h_*shield*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=1.0]/map/shields/" + fileCopyDetails.path.replaceAll(/h_(.*shield.*)\.png/, '$1.png')
		}
	}
	from("../../../resources/rendering_styles/style-icons/drawable-mdpi") {
		include "h_*.png"
		exclude "h_*shield*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=1.0]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
		}
	}
	from("../../../resources/rendering_styles/style-icons/drawable-mdpi") {
		include "mm_*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=1.0]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
		}
	}

	// Map icons (Android hdpi == 1.5 ddf):
	from("../../../resources/rendering_styles/style-icons/drawable-hdpi") {
		include "h_*shield*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=1.5]/map/shields/" + fileCopyDetails.path.replaceAll(/h_(.*shield.*)\.png/, '$1.png')
		}
	}
	from("../../../resources/rendering_styles/style-icons/drawable-hdpi") {
		include "h_*.png"
		exclude "h_*shield*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=1.5]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
		}
	}
	from("../../../resources/rendering_styles/style-icons/drawable-hdpi") {
		include "mm_*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=1.5]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
		}
	}

	// Map icons (Android xhdpi == 2.0 ddf):
	from("../../../resources/rendering_styles/style-icons/drawable-xhdpi") {
		include "h_*shield*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=2.0]/map/shields/" + fileCopyDetails.path.replaceAll(/h_(.*shield.*)\.png/, '$1.png')
		}
	}
	from("../../../resources/rendering_styles/style-icons/drawable-xhdpi") {
		include "h_*.png"
		exclude "h_*shield*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=2.0]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
		}
	}
	from("../../../resources/rendering_styles/style-icons/drawable-xhdpi") {
		include "mm_*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "[ddf=2.0]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
		}
	}

	// Misc map resources:
	from("../../../resources/rendering_styles/stubs") {
		include "**/*.png"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "map/stubs/" + fileCopyDetails.path
		}
	}

	// Routing:
	from("../../../resources/routing") {
		include "routing.xml"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "routing/" + fileCopyDetails.path
		}
	}

	// Fonts:
	from("../../../resources/rendering_styles/fonts") {
		include "**/*.ttf"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "map/fonts/" + fileCopyDetails.path
		}
	}

	// Misc resources
	from("../../../resources/misc/icu4c") {
		include "*.dat"
		eachFile { fileCopyDetails ->
			fileCopyDetails.path = "misc/icu4c/" + fileCopyDetails.path.replaceAll(/icudt\d+([lb])\.dat/, 'icu-data-$1.dat')
		}
	}

	into "./assets/OsmAndCore_ResourcesBundle"
	includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn << copyOsmAndResources
}

dependencies {
	compile fileTree(dir: "libs", include: ["*.jar"])
}
